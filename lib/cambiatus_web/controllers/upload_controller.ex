defmodule CambiatusWeb.UploadController do
  @moduledoc false

  use CambiatusWeb, :controller

  alias Cambiatus.FileUploader, as: Uploader

  # This potential directory traversal vulnerability is ignored because the file beign
  # read is generated by Phoenix. The file creation cannot be influenced by user input

  # sobelow_skip ["Traversal.FileModule"]
  def save(conn, params) do
    with %{path: file_path, content_type: content_type} <- Map.get(params, "file"),
         %{path: file_path} <-
           Uploader.resize(file_path, content_type, 1200, 1200, in_place: true),
         {:ok, file_path} <- Uploader.strip_metadata(file_path),
         file_info <- File.lstat!(file_path),
         file_contents <- File.read!(file_path),
         {:ok, url} <- Uploader.save(file_info, content_type, file_contents) do
      conn
      |> put_status(200)
      |> json(%{
        data: url
      })
    else
      nil ->
        conn
        |> put_status(422)
        |> json(%{error: "File is required"})

      {:error, err} ->
        conn
        |> put_status(500)
        |> json(%{error: err})
    end
  end
end
